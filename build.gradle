buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:7.2.1'
    }
}

plugins {
    id 'idea'
    id 'groovy'
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.owasp.dependencycheck' version '8.0.2'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
group = 'io.elastic'
version = '4.0.4-SNAPSHOT.9'
sourceCompatibility = '1.8'

 dependencyCheck {
     format = 'ALL'
     // Dependency Check script will fail in case there are 8.0+ vulnerabilities.
     // It should be configured to 7 (high and critical), but so far is not possible as 'jakarta.json-api' library
     // does not have any updates that solve the problem
     failBuildOnCVSS = 8
     suppressionFile = './dependencycheck-base-suppression.xml'
 }
 // check.dependsOn dependencyCheckAnalyze

def isCiEnv = 'true'.equals(System.getenv('CI'))
def isCiTag = System.getenv('CIRCLE_TAG') != null;
def isReleaseBuild = !version.contains('SNAPSHOT');
def snapshotRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
def releaseRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
def repositoryUsername = project.hasProperty('sonatypeUsername') ? sonatypeUsername : ""
def repositoryPassword = project.hasProperty('sonatypePassword') ? sonatypePassword : ""

repositories {
    mavenCentral()
    maven {
        url snapshotRepositoryUrl
    }
}

dependencies {
    testRuntimeOnly 'org.codehaus.groovy:groovy-all:3.0.22'
    testImplementation 'cglib:cglib-nodep:3.3.0'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'org.spockframework:spock-junit4:2.2-groovy-3.0'
    testImplementation 'org.spockframework:spock-core:2.2-groovy-3.0'
    testImplementation 'com.github.rest-driver:rest-client-driver:2.0.1'
    testImplementation 'org.eclipse.jetty:jetty-server:9.4.55.v20240627'
    testImplementation 'com.github.tomakehurst:wiremock-jre8-standalone:2.35.2'

    api 'org.eclipse.parsson:parsson:1.1.6'
    api 'com.rabbitmq:amqp-client:5.21.0'
    api 'commons-codec:commons-codec:1.15'
    api 'commons-io:commons-io:2.16.1'
    api 'org.apache.httpcomponents:httpclient:4.5.14'
    api 'com.google.guava:guava:33.2.1-jre'
    api 'com.google.inject:guice:7.0.0'
    api 'com.google.inject.extensions:guice-assistedinject:7.0.0'
    api 'ch.qos.logback:logback-classic:1.2.13'
    api 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
    api 'ch.qos.logback.contrib:logback-jackson:0.1.5'
    api 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
}

jar {
    manifest {
        attributes(
                'Implementation-Vendor' : 'elastic.io Gmbh',
                'Implementation-Title': 'elastic.io Sailor for JVM',
                'Implementation-Version': archiveVersion,
                'Multi-Release': true
        )
    }
}
test {
   useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

wrapper {
    gradleVersion = '7.5.1'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'sailor-jvm'
            from components.java
            pom {
                name = 'elasticio-sailor-jvm'
                packaging = 'jar'
                description = 'Sailor for JVM'
                url = 'http://www.elastic.io'
                inceptionYear = '2015'

                organization {
                    name = 'elastic.io GmbH'
                    url = 'http://www.elastic.io'
                }

                scm {
                    url = 'scm:git@github.com:elasticio/sailor-jvm.git'
                    connection = 'scm:git@github.com:elasticio/sailor-jvm.git'
                    developerConnection = 'scm:git@github.com:elasticio/sailor-jvm.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/elasticio/sailor-jvm/issues'
                }

                developers {
                    developer {
                        id = 'elasticio'
                        name = 'elastic.io GmbH'
                        email = 'support@elastic.io'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = isReleaseBuild ? releaseRepositoryUrl : snapshotRepositoryUrl
            credentials {
                username = repositoryUsername
                password = repositoryPassword
            }
        }
    }

    // A check to disable publishing if it's a snapshot version
    if (!isReleaseBuild) {
        println 'This is a SNAPSHOT version. Publishing to the Maven Snapshot will be skipped.'
        tasks.withType(PublishToMavenRepository) { task ->
            task.enabled = false
        }
    }
}

signing {
    println 'isReleaseBuild: ' + isReleaseBuild;
    required { isReleaseBuild && gradle.taskGraph.hasTask("publish"); }
    if(isReleaseBuild && project.hasProperty('signingPassword') && project.hasProperty('signingKeyBase64')) {
        def signingPassword = findProperty("signingPassword")
        def signingKey = new String(findProperty("signingKeyBase64").decodeBase64())
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
    sign publishing.publications.mavenJava
}

afterEvaluate {
    //  In case CI env - skip publishing of release version without created tag
    tasks.withType(PublishToMavenRepository) { task ->
        if (isReleaseBuild && isCiEnv && !isCiTag) {
            println 'Can not publish release version without tag. Did not find CIRCLE_TAG env variable. Skipping publication'
            task.enabled = false
        }
    }
}

task postPublishCopyFilesFromMavelLocal(type: Copy) {
    dependsOn publishToMavenLocal
    def homeDir = System.getProperty("user.home")
    def sourceDir = "${homeDir}/.m2/repository/io/elastic/sailor-jvm/${project.version}"
    from fileTree(dir: sourceDir, include: '**/*', exclude: 'maven-metadata-local.xml')
    into "${projectDir}/build/sailorReleaseBundle"
}

task postPublishCreateBundleJarFromMavelLocal(type: Exec) {
    dependsOn publishToMavenLocal
    commandLine "bash", "-c", "cd ${projectDir}/build/sailorReleaseBundle && jar -cvf bundle.jar ./*"
}

task postPublishActions {
    dependsOn publishToMavenLocal, postPublishCopyFilesFromMavelLocal, postPublishCreateBundleJarFromMavelLocal
    doLast {
        println '====================================================================================================='
        println 'All done. You can go to ./build/sailorReleaseBundle and upload the bundle.jar file from there'
        println '====================================================================================================='
    }
}

// Define the task dependencies and ensure they run in order
postPublishCopyFilesFromMavelLocal.mustRunAfter publishToMavenLocal
postPublishCreateBundleJarFromMavelLocal.mustRunAfter postPublishCopyFilesFromMavelLocal

// Add the custom tasks to the lifecycle
publishToMavenLocal.finalizedBy postPublishActions

shadowJar {
    archiveBaseName.set('sailor-jvm')
    archiveVersion.set(project.version.toString())
    archiveClassifier.set('')
}

task printVersion {
    doLast {
        println "Sailor JVM version: ${project.version}"
    }
}
